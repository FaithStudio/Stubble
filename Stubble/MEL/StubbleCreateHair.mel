//Error message
$error = "";
//First store all selected objects
$selected = `ls -sl`;
//For every selected object
for ( $obj in $selected )
{
	// Does it have mesh ?
	if( `objExists ( $obj +".worldMesh" )` )
	{
		// Create our locator
		$shape = `createNode "HairShape"`;
		// Connect selected object mesh to our shape surface
		connectAttr ( $obj + ".worldMesh" ) ( $shape + ".surface" );
		// Connect time to our shape time
		connectAttr "time1.outTime" ( $shape + ".time" );
		// Lock transform attributes of shape
		$parent = `listRelatives -p $shape`;
		setAttr -l true ( $parent[0] + ".translate" );
		setAttr -l true ( $parent[0] + ".rotate" );
		setAttr -l true ( $parent[0] + ".scale" );
		setAttr -l true ( $parent[0] + ".shear" );

		// If mental ray is loaded, ...
		if ( `pluginInfo -query -loaded -name "Mayatomr"` )
		{
			// And if the stubble_geometry shader exists, ...
			if ( stringArrayContains("stubble_geometry", `listNodeTypes "rendernode/mentalray"`) )
			{
				// Create a proxy sphere as a child of HairShape transform node and connect our geometry shader to it
				$MRsphere = `polySphere -r 0.001 -sx 4 -sy 3`;
				parent $MRsphere[0] $parent[0];
				setAttr ($MRsphere[0] + ".miExportGeoShader") 1;
				$geometry_shader = `createNode "stubble_geometry"`;
				connectAttr -f ($geometry_shader + ".outValue") ($MRsphere[0] + ".miGeoShader");
			}
			else
			{
				warning "Couldn't find the \"stubble_geometry\" shader. Please check the Stubble installation.";
			}

			// Reselect the hair
			select -r $shape;
		}
	}
	else
	{
		$error += $obj + " does not have mesh\n";
	}
}
// Any error occured ?
if ( $error != "" )
{
	error( $error );
}
